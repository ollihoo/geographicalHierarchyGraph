import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.2.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = '1.0'
def springBootVersion = '1.4.2.RELEASE'
def boltVersion = '2.0.5'
group = 'ollihoo'

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/snapshot" }
    mavenLocal()
}

jar {
    baseName = 'geographicalHierarchy'
    version = '0.0.1-SNAPSHOT'
}

dependencies {
    compile('org.codehaus.groovy:groovy-all:2.4.7')
    compile("org.springframework.boot:spring-boot-starter:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf:$springBootVersion")
    compile("org.springframework.data:spring-data-neo4j:4.1.5.RELEASE")
    compile("org.neo4j:neo4j-ogm-api:$boltVersion")
    compile("org.neo4j:neo4j-ogm-core:$boltVersion")
    compile("org.neo4j:neo4j-ogm-http-driver:$boltVersion")
    compile("org.neo4j:neo4j-ogm-embedded-driver:$boltVersion")
    compile("org.neo4j:neo4j-ogm-bolt-driver:$boltVersion")

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    testCompile("org.springframework.boot:spring-boot-test-autoconfigure:${springBootVersion}")
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4-rc-3')
    testCompile('cglib:cglib-nodep:3.2.4')
}

task copyDockerTemplates(type: Copy, dependsOn: 'build') {
    from "src/main/docker"
    into "build/docker"
    filter(ReplaceTokens, tokens: [applicationVersion: applicationVersion])
}

task copyJarToDocker(type: Copy, dependsOn: 'copyDockerTemplates') {
    from "build/libs"
    into "build/docker"
}

task docker(dependsOn: 'copyJarToDocker') {
    doLast {
        println "Current version is ################## ${applicationVersion} #################"
    }
}

task dockerBuild(type: Exec, dependsOn: 'docker') {
    workingDir 'build/docker'
    commandLine 'docker', 'build', '-t', 'ollihoo/geographicalhierarchygraph:latest', './'
}

task dockerLogin(type: Exec) {
    workingDir 'build/docker'
    commandLine 'docker', 'login', '-u', "$dockerUsername", '-p', "$dockerPassword"
}

task dockerPush(type: Exec, dependsOn: 'dockerBuild') {
    workingDir 'build/docker'
    commandLine 'docker', 'push', 'ollihoo/geographicalhierarchygraph:latest'
}

task dockerLogout(type: Exec) {
    workingDir 'build/docker'
    commandLine 'docker', 'logout'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}